#! /usr/bin/perl

use lib qw( /usr/local/freescore/lib );
use Clone qw( clone );
use Data::Structure::Util qw( unbless );
use Mojolicious::Lite;
use Mojo::IOLoop::Delay;
use JSON::XS;
use Digest::SHA1 qw( sha1_hex );
use FreeScore::Config;
use FreeScore::Feats::Breaking::Division;
use FreeScore::Feats::Breaking;
use FreeScore::Feats::Breaking::RequestManager;
use Try::Tiny;
use PHP::Session;
use Data::Dumper;
use List::MoreUtils qw( first_index );

srand();

our $json   = new JSON::XS();
our $config = new FreeScore::Config();
our $client = {};

# ============================================================
websocket '/breaking/:tournament/:ring' => sub {
# ============================================================
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring'       );
	my $manager    = new FreeScore::Feats::Breaking::RequestManager( $tournament, $ring, $self->tx() );
	my $progress   = undef;
	my $staging    = undef;
	my $id         = $self->cookie( 'freescore-session' );
	my $group      = $ring eq 'staging' ? "$tournament-staging" : sprintf "%s-ring-%d", $tournament, $ring;

	$self->inactivity_timeout( 3600 ); # 1 hour

	# ===== REGISTER THE CLIENT GROUP
	my $c = $client->{ $group }{ $id } = { id => $id, device => $self->tx() }; 

	# ----------------------------------------
	# Handle messages
	# ----------------------------------------
	$self->on( message => sub {
		my $self    = shift;
		my $request = $json->decode( shift );

		$request->{ tournament } = $tournament;
		$request->{ ring }       = $ring;

		# ===== READ PROGRESS
		try   { 
			$progress = new FreeScore::Feats::Breaking( $tournament, $ring ); 
		} catch { $c->{ device }->send( { json => { error => "Error reading database '$tournament', ring $ring: $_" }}); };

		# ===== HANDLE REQUEST
		my $clients = $client->{ $group };
		try   { $manager->handle( $request, $progress, $clients ); }
		catch { $c->{ device }->send( { json => { error => "Error while processing request: $_\n", request => $request }}); };
	});
};

# ============================================================
# JUDGE INTERFACE
# ============================================================

# ------------------------------------------------------------
# GET THE NUMBER OF JUDGES FOR THIS DIVISION
# ------------------------------------------------------------
get '/:tournament/:ring/judges' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );

	try {
		my $progress   = new FreeScore::Feats::Breaking( $tournament, $ring );
		my $division   = $progress->current();
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { 'division' => $division->{ name }, 'judges' => $division->{ judges } } );

	} catch {
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { error => $_ });
	}
};

# ============================================================
# ADMIN INTERFACE
# ============================================================

# ------------------------------------------------------------
# GET THE DIVISION INFORMATION AS JSON
# ------------------------------------------------------------
get '/:tournament/:ring/:divid/json' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $divid      = $self->param( 'divid' );

	try {
		my $progress   = new FreeScore::Feats::Breaking( $tournament, $ring );
		my $found      = $progress->find( $divid );
		my $division   = $found ? $found : $progress->current();
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( text => $division->to_json(), format => 'json' );

	} catch {
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { error => $_ });
	}
};

# ------------------------------------------------------------
# GET THE DIVISION INFORMATION AS CSV
# ------------------------------------------------------------
get '/:tournament/:ring/:divid/csv' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $divid      = $self->param( 'divid' );

	try {
		my $progress   = new FreeScore::Feats::Breaking( $tournament, $ring );
		my $found      = $progress->find( $divid );
		my $division   = $found ? $found : $progress->current();
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( text => $division->to_csv(), format => 'txt' );

	} catch {
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { error => $_ });
	}
};

# ------------------------------------------------------------
# WRITE NEW TOURNAMENT INFORMATION
# ------------------------------------------------------------
post '/:tournament/:ring/:divid' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $divid      = $self->param( 'divid' );
	my $json       = new JSON::XS();
	my $data       = $json->decode( $self->req->body());
	my $progress   = new FreeScore::Feats::Breaking( $tournament, $ring );
	my $division   = $progress->find( $divid );

	my $path  = sprintf( "%s/%s/%s/ring%02d", $FreeScore::PATH, $tournament, $FreeScore::Feats::Breaking::SUBDIR, $ring );
	$division = new FreeScore::Feats::Breaking::Division( $path, $divid, $ring ) unless( $division );

	try {
		if( $division->exists() && ! exists $data->{ overwrite } ) {
			$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
			$self->render( json => { division => $divid, status => 'exists' } );
			return;
		}
		foreach my $key (keys %{ $data->{ header }}) { 
			$data->{ header }{ $key } = $json->decode( $data->{ header }{ $key } ) if( $key eq 'brackets' );
			$division->{ $key } = $data->{ header }{ $key }; 
		}
		delete $division->{ mode } if( ! exists $data->{ header }{ mode } );
		my $athletes = [];
		foreach my $i ( 0 .. $#{ $data->{ athletes }} ) {
			my $athlete = { name => $data->{ athletes }[ $i ], index => $i, info => undef, scores => [] };
			my $j       = first_index { lc $_->{ name } eq lc $athlete->{ name } } @{ $division->{ athletes }};
			if( $j >= 0 ) { 
				my $found = $division->{ athletes }[ $j ];
				foreach my $key (qw( info scores )) { $athlete->{ $key } = $found->{ $key }; }
			}
			push @$athletes, $athlete;
		}
		$division->{ athletes } = $athletes;
		$division->write();
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { division => $divid, status => 'saved' } );

	} catch {
		$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
		$self->render( json => { 'error' => $_ } );
	}
};

# ============================================================
# STATUS
# ============================================================
get '/status' => sub {
	my $self = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( text => 'OK' );
};

# ============================================================
# ERROR TRAPPING
# ============================================================
any '*' => sub {
	my $self = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => $config->host() );
	$self->render( json => { error => 'FreeScore Breaking Unknown command' } );
};

# ============================================================
sub autopilot {
# ============================================================
	my $self     = shift;
	my $progress = shift;
	my $division = shift;
	my $delay    = new Mojo::IOLoop::Delay();
	my $pause    = { score => 9, leaderboard => 5, brief=> 4, next => 1 };

	my $show  = {
		score => sub {
			my $delay = shift;
			Mojo::IOLoop->timer( $pause->{ score } => $delay->begin() );
			if ( $division->is_display() ) { $division->score(); }
			$division->write();
		},
		leaderboard => sub {
			my $delay = shift;
			Mojo::IOLoop->timer( $pause->{ leaderboard } => $delay->begin() );
			if ( $division->is_score() ) { $division->display(); }
			$division->write();
		},
		next => sub {
			my $delay = shift;
			Mojo::IOLoop->timer( $pause->{ next } => $delay->begin() );
			if ( $division->is_display() ) { $division->score(); }
			$division->write();
		}
	};
	my $go = {
		next => sub {
			my $delay = shift;
			Mojo::IOLoop->timer( $pause->{ brief } => $delay->begin() );
			$division->next();
			$division->write();
		}
	};

	my @steps = ( $show->{ score }, $show->{ leaderboard } );
	push @steps, $go->{ next }, $show->{ next } unless( $division->{ complete } );

	$delay->steps( @steps )->catch( sub {} )->wait();
}

# ============================================================
# HYPNOTOAD SERVER
# ============================================================
mkdir '/var/log/freescore' unless -e '/var/log/freescore';
app->config( hypnotoad => { listen => [ 'http://*:3078' ], pid_file => '/var/run/breaking.pid', workers => 1 });
app->log( new Mojo::Log( path => '/var/log/freescore/breaking.log', level => 'error' ));
app->start();
